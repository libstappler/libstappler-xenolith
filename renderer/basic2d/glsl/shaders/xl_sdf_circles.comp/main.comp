#version 450
#extension GL_GOOGLE_include_directive : enable

#include "XL2dGlslVertexData.h"
#include "XL2dGlslShadowData.h"
#include "XL2dGlslSdfData.h"

layout (local_size_x = 64) in;

#include "XL2dGlslSdfDescriptors.h"

void main() {
	const uint gID = gl_GlobalInvocationID.x;

    if (gID < shadowData.circlesCount) {
		Sdf2DObjectData circle;
		circle.object = gID + shadowData.circlesFirst;
		circle.type = 1;

		const Circle2DIndex idx = CIRCLE_INDEX_BUFFER[gID];
		const mat4 transform = inverse(TRANSFORM_BUFFER[idx.transform].transform);
		const vec4 scale = TRANSFORM_BUFFER[idx.transform].padding;
		const float radius = VERTEX_BUFFER[idx.origin].w;
		const vec4 origin = vec4(VERTEX_BUFFER[idx.origin].xyz, 1);

		circle.a = VERTEX_BUFFER[idx.origin].xy * shadowData.shadowDensity * shadowData.density;
		circle.b = (radius * shadowData.shadowDensity).xx;
		circle.value = idx.value;
		circle.opacity = idx.opacity;
		circle.transform = idx.transform;

		vec4 radiusX1 = transform * (origin + vec4(radius, radius, 0, 0)) * shadowData.shadowDensity;
		vec4 radiusX2 = transform * (origin + vec4(-radius, -radius, 0, 0)) * shadowData.shadowDensity;
		vec4 radiusY1 = transform * (origin + vec4(-radius, radius, 0, 0)) * shadowData.shadowDensity;
		vec4 radiusY2 = transform * (origin + vec4(radius, -radius, 0, 0)) * shadowData.shadowDensity;

		circle.bbMin = vec2(min(min(radiusX1.xy, radiusX2.xy), min(radiusY1.xy, radiusY2.xy))) - shadowData.bbOffset.xx;
		circle.bbMax = vec2(max(max(radiusX1.xy, radiusX2.xy), max(radiusY1.xy, radiusY2.xy))) + shadowData.bbOffset.xx;

		ivec2 minCell = ivec2(trunc(circle.bbMin / vec2(shadowData.gridSize, shadowData.gridSize)));
		ivec2 maxCell = ivec2(floor(circle.bbMax / vec2(shadowData.gridSize, shadowData.gridSize)));

		for (int i = minCell.x; i <= maxCell.x; ++ i) {
			for (int j = minCell.y; j <= maxCell.y; ++ j) {
				emplaceIntoGrid(i, j, circle.object);
			}
		}

		OBJECTS_DATA_BUFFER[circle.object] = circle;
    }
}
